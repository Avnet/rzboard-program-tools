; Copyright (C) 2022 Avnet
; This TeraTerm macro programs the bootloader images to QSPI flash or eMMC.

; +----------------------------------+
; |         Macro Parameters         |
; +----------------------------------+

; #### You probably need to modify the COM port here ####
COM = 7

; #### You probably need to modify the target here ####
; Program QSPI or eMMC: 1->QSPI  0->eMMC
QSPI_NOT_eMMC = 1

; #### Normally you do not need to modify anything below this line ####

; Bootloader image files.
FLASH_WRITER  = "images/flashwriter_rzboard.mot"
FILE_BL2 = "images/bl2_bp-rzboard.srec"
FILE_FIP = "images/fip-rzboard.srec"

; get current base path
getdir BASE_PATH
strconcat BASE_PATH "\"

; +----------------------------------+
; |       Connect to comport         |
; +----------------------------------+

comstr='/C='
int2str comport COM
strconcat comstr comport

connect comstr

testlink
if result=0 then
   messagebox "No COM port found!" "Error!"
   disconnect 0
   closett
   end
   exit
endif
if result=1 then
   messagebox "Connect to COM port failed, is the COM port already open?" "Error!"
   disconnect 0
   closett
   end
   exit
endif

; set baudrate to 115200 bps
setbaud 115200

setsync 1

; +--------------------------+
; |  Download Flash Writer   |
; +--------------------------+

IMG_PATH=BASE_PATH
strconcat IMG_PATH FLASH_WRITER

waitregex "please send !"
sendfile IMG_PATH 0
waitregex ">"  
 

; +--------------------------+
; |      Do Flash Erase      |
; +--------------------------+
 
if QSPI_NOT_eMMC then
   sendln "xcs"   
   waitregex "Clear OK?"
   sendln "y"   
else
   sendln "EM_E"
   waitregex "Select area"
   sendln "1"
endif

waitregex ">"   

; +--------------------------+
; |      Flash BL2 Image     |
; +--------------------------+

IMG_PATH=BASE_PATH
strconcat IMG_PATH FILE_BL2

if QSPI_NOT_eMMC then
  sendln "XLS2"
   
  waitregex "Please Input : H'"
  sendln "11E00"
   
  waitregex "Please Input : H'"
  sendln "00000"   
   
  waitregex "please send !"   
  sendfile IMG_PATH 0
else
   sendln "EM_W"

   waitregex "Select area"
   sendln "1"

   waitregex "Please Input Start Address in sector"
   sendln "1"

   waitregex "Please Input Program Start Address"
   sendln "11E00"

   waitregex "please send"
   sendfile IMG_PATH 0
endif
  
waitregex ">"   

; +--------------------------+
; |      Flash FIP Image     |
; +--------------------------+

IMG_PATH=BASE_PATH
strconcat IMG_PATH FILE_FIP

if QSPI_NOT_eMMC then
  sendln "XLS2"
   
  waitregex "Please Input : H'"
  sendln "00000"
   
  waitregex "Please Input : H'"
  sendln "1D200"   
   
  waitregex "please send"   
  sendfile IMG_PATH 0
  
else 

  sendln "EM_W"
   
  waitregex "Select area"
  sendln "1"
   
  waitregex "Please Input Start Address in sector"
  sendln "100"   

  waitregex "Please Input Program Start Address"
  sendln "00000"
   
  waitregex "please send"   
  sendfile IMG_PATH 0
  waitregex "EM_W Complete!"
   
  sendln "EM_SECSD"
  waitregex "Please Input EXT_CSD Index"
   
  sendln "B1"
  waitregex "Please Input Value"
  sendln "2"
  waitregex ">"  
   
  sendln "EM_SECSD"
  waitregex "Please Input EXT_CSD Index"
   
  sendln "B3"
  waitregex "Please Input Value"
  sendln "8"
endif
  
waitregex ">"  

; +--------------------------+
; |    Disconnect And Exit   |
; +--------------------------+

disconnect 0
closett
end
exit
