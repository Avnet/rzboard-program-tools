; Copyright (C) 2022 Avnet
; This TeraTerm macro programs the linux system image by fastboot in U-boot.

; +----------------------------------+
; |         Macro Parameters         |
; +----------------------------------+

COM = params[2]
IPADDR = params[3]
WORK_DIR = params[4]
SYSIMG_FILE  = params[5]

; connection method: 0->Ethernet  1->USB
CONN_MODE = params[6]

; get current base path
getdir BASE_PATH
strconcat BASE_PATH "\"

; +----------------------------------+
; |       Connect to comport         |
; +----------------------------------+

comstr='/C='
strconcat comstr COM

connect comstr

testlink
if result=0 then
   messagebox "No COM port found!" "Error!"
   disconnect 0
   closett
   end
   exit
endif
if result=1 then
   messagebox "Connect to COM port failed, is the COM port already open?" "Error!"
   disconnect 0
   closett
   end
   exit
endif

; set baudrate to 115200 bps
setbaud 115200

setsync 1

; +-------------------------------------+
; |        Setting U-boot Env           |
; +-------------------------------------+

SCRIPT_CMD=BASE_PATH
strconcat SCRIPT_CMD "fastboot.bat " 
strconcat SCRIPT_CMD IPADDR 
strconcat SCRIPT_CMD " " 
strconcat SCRIPT_CMD WORK_DIR 
strconcat SCRIPT_CMD " " 
strconcat SCRIPT_CMD SYSIMG_FILE 
strconcat SCRIPT_CMD " " 
strconcat SCRIPT_CMD CONN_MODE 

waitregex "Hit any key to stop autoboot:"
sendln "y"

flushrecv
mpause 100

strcompare CONN_MODE '1'
if result=0 then
   sendln 'setenv serial# ' 'Renesas-rzv'
   sendln "fastboot usb 0"
   mpause 100
else
   sendln 'setenv ipaddr ' IPADDR
   sendln "fastboot udp"
   waitregex "Listening for fastboot command on"
endif

exec SCRIPT_CMD

waitregex "command oem flashdone"
; +--------------------------+
; |    Disconnect And Exit   |
; +--------------------------+

disconnect 0
closett
end
exit
