; Copyright (C) 2022 Avnet
; This TeraTerm macro programs the bootloader images to QSPI flash or eMMC.

; +----------------------------------+
; |         Macro Parameters         |
; +----------------------------------+

COM = params[2]

; Program QSPI or eMMC: 1->QSPI  0->eMMC
str2int QSPI_NOT_eMMC params[3]

IMG_DIR = params[4]
strconcat IMG_DIR "\images\"

FLASH_WRITER  = params[5]
FILE_BL2 = params[6]
FILE_FIP = params[7]


; +----------------------------------+
; |       Connect to comport         |
; +----------------------------------+

comstr='/C='
strconcat comstr COM

connect comstr

testlink
if result=0 then
   messagebox "No COM port found!" "Error!"
   disconnect 0
   closett
   end
   exit
endif
if result=1 then
   messagebox "Connect to COM port failed, is the COM port already open?" "Error!"
   disconnect 0
   closett
   end
   exit
endif

; set baudrate to 115200 bps
setbaud 115200

setsync 1

; +--------------------------+
; |  Download Flash Writer   |
; +--------------------------+

IMG_PATH=IMG_DIR
strconcat IMG_PATH FLASH_WRITER

waitregex "please send !"
sendfile IMG_PATH 0
waitregex ">"  

; +--------------------------+
; |      Do Flash Erase      |
; +--------------------------+
 
if QSPI_NOT_eMMC then
   dispstr " "#$0A
   dispstr " Start the erase operation, wait for about 5 minutes...   "
   sendln "xcs"   
   waitregex "Clear OK?"
   sendln "y"   
else
   sendln "EM_E"
   waitregex "Select area"
   sendln "1"
endif

waitregex ">"   

; +--------------------------+
; |      Flash BL2 Image     |
; +--------------------------+

IMG_PATH=IMG_DIR
strconcat IMG_PATH FILE_BL2

if QSPI_NOT_eMMC then
  sendln "XLS2"
   
  waitregex "Please Input : H'"
  sendln "11E00"
   
  waitregex "Please Input : H'"
  sendln "00000"   
   
  waitregex "please send !"   
  sendfile IMG_PATH 0
else
   sendln "EM_W"

   waitregex "Select area"
   sendln "1"

   waitregex "Please Input Start Address in sector"
   sendln "1"

   waitregex "Please Input Program Start Address"
   sendln "11E00"

   waitregex "please send"
   sendfile IMG_PATH 0
endif
  
waitregex ">"   

; +--------------------------+
; |      Flash FIP Image     |
; +--------------------------+

IMG_PATH=IMG_DIR
strconcat IMG_PATH FILE_FIP

if QSPI_NOT_eMMC then
  sendln "XLS2"
   
  waitregex "Please Input : H'"
  sendln "00000"
   
  waitregex "Please Input : H'"
  sendln "1D200"   
   
  waitregex "please send"   
  sendfile IMG_PATH 0
  
else 

  sendln "EM_W"
   
  waitregex "Select area"
  sendln "1"
   
  waitregex "Please Input Start Address in sector"
  sendln "100"   

  waitregex "Please Input Program Start Address"
  sendln "00000"
   
  waitregex "please send"   
  sendfile IMG_PATH 0
  waitregex "EM_W Complete!"
   
  sendln "EM_SECSD"
  waitregex "Please Input EXT_CSD Index"
   
  sendln "B1"
  waitregex "Please Input Value"
  sendln "2"
  waitregex ">"  
   
  sendln "EM_SECSD"
  waitregex "Please Input EXT_CSD Index"
   
  sendln "B3"
  waitregex "Please Input Value"
  sendln "8"
endif
  
waitregex ">"  

; +--------------------------+
; |    Disconnect And Exit   |
; +--------------------------+

disconnect 0
closett
end
exit
